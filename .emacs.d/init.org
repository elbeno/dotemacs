* OS Specifics
** Default values
*** Default font = inconsolata
#+begin_src emacs-lisp
(set-face-attribute 'default nil
                    :family "Inconsolata"
                    :height 100
                    :weight 'normal
                    :width 'normal)

(set-fontset-font "fontset-default"
                  '(#x0100 . #xffff)
                  (font-spec :family "DejaVu Sans Mono"
                             :height 100
                             :weight 'normal
                             :width 'normal))
#+end_src
*** Path to cedet
#+begin_src emacs-lisp
(setq cedet-dir "/home/bdeane/dev/cedet/")
#+end_src
*** Column to wrap at
#+begin_src emacs-lisp
(setq column-wrap-soft 80)
(setq column-wrap-hard 100)
#+end_src
*** Use SBCL
#+begin_src emacs-lisp
(setq inferior-lisp-program "sbcl")
#+end_src
*** Monitor width and height
#+begin_src emacs-lisp
(setq primary-monitor-width 1920)
(setq primary-monitor-height 1200)
#+end_src
** Mac
*** Alt = meta
#+begin_src emacs-lisp
(if (eq system-type 'darwin)
    (setq mac-command-modifier 'meta))
#+end_src
** Windows
*** Get the clipboard to work
#+begin_src emacs-lisp
(if (eq system-type 'windows-nt)
    (set-clipboard-coding-system 'utf-16le-dos))
#+end_src
*** Use Consolas
#+begin_src emacs-lisp
(if (eq system-type 'windows-nt)
    (progn
      (set-face-attribute 'default nil
                          :family "Consolas"
                          :height 130
                          :weight 'normal
                          :width 'normal)
      (set-fontset-font "fontset-default"
                        '(#x0100 . #xffff)
                        (font-spec :family "Arial Unicode MS"
                                   :height 100
                                   :weight 'normal
                                   :width 'normal))))
#+end_src
*** Path overrides
#+begin_src emacs-lisp
(if (eq system-type 'windows-nt)
    (setq cedet-dir "e:/projects/cedet/"))
#+end_src
*** Use CLISP
#+begin_src emacs-lisp
(if (eq system-type 'windows-nt)
    (setq inferior-lisp-program "clisp.exe"))
#+end_src
*** Monitor width and height
#+begin_src emacs-lisp
(setq primary-monitor-width 1920)
(setq primary-monitor-height 1125) ; allow for bottom bar
#+end_src
** Cygwin
*** Get the clipboard to work
#+begin_src emacs-lisp
(if (eq system-type 'cygwin)
    (set-clipboard-coding-system 'utf-16le-dos))
#+end_src
*** Use Consolas
#+begin_src emacs-lisp
(if (eq system-type 'cygwin)
    (progn
      (set-face-attribute 'default nil
                          :family "Consolas"
                          :height 100
                          :weight 'normal
                          :width 'normal)
      (set-fontset-font "fontset-default"
                        '(#x0100 . #xffff)
                        (font-spec :family "Arial Unicode MS"
                                   :height 100
                                   :weight 'normal
                                   :width 'normal))))
#+end_src
*** Path overrides
#+begin_src emacs-lisp
(if (eq system-type 'cygwin)
    (setq cedet-dir "/cygdrive/e/projects/cedet/"))
#+end_src
*** Use CLISP
#+begin_src emacs-lisp
(if (eq system-type 'cygwin)
    (setq inferior-lisp-program "clisp.exe"))
#+end_src
*** Monitor width and height
#+begin_src emacs-lisp
(setq primary-monitor-width 1920)
(setq primary-monitor-height 1125) ; allow for bottom bar
#+end_src
* Basic things
** Tidier UI
*** Turn off toolbar, menu bar, etc
#+begin_src emacs-lisp
(tool-bar-mode -1)
(menu-bar-mode -1)
(display-battery-mode -1)
(scroll-bar-mode -1)
#+end_src
*** Inhibit splash screen and start with empty scratch buffer
#+begin_src emacs-lisp
(setq inhibit-startup-screen t)
(setq initial-scratch-message "")
#+end_src
*** Prevent annoying beep - use visible bell
#+begin_src emacs-lisp
(setq visible-bell 1)
#+end_src
** Frame
#+begin_src emacs-lisp
(setq default-frame-height (/ primary-monitor-height (frame-char-height)))
(setq frame-title-format "%b [emacs]")
(setq default-frame-alist
  (append
    `((width . ,column-wrap-hard)
      (height . ,default-frame-height))
    default-frame-alist))
#+end_src
** Coding system UTF-8
#+begin_src emacs-lisp
(prefer-coding-system 'utf-8)
(set-default-coding-systems 'utf-8)
(set-terminal-coding-system 'utf-8)
(set-keyboard-coding-system 'utf-8)
(set-selection-coding-system 'utf-8)
(setq default-buffer-file-coding-system 'utf-8)
(setq locale-coding-system 'utf-8)
#+end_src
** Autosaves/backups
*** Move autosave/backup files to ~/.emacs.d/autosaves and ~/.emacs.d/backups

#+begin_src emacs-lisp
(setq backup-directory-alist
      `((".*" . , "~/.emacs.d/backups/")))
(setq auto-save-file-name-transforms
      `((".*" ,"~/.emacs.d/autosaves/" t)))
#+end_src

*** Ensure directory exists
#+begin_src emacs-lisp
(make-directory "~/.emacs.d/backups" t)
(make-directory "~/.emacs.d/autosaves" t)
#+end_src
*** Remove old backups
Remove backups older than one week

#+begin_src emacs-lisp
(message "Deleting old backup files...")
(let ((week (* 60 60 24 7))
      (current (float-time (current-time))))
  (dolist (file (directory-files temporary-file-directory t))
    (when (and (backup-file-name-p file)
               (> (- current (float-time (fifth (file-attributes file))))
                  week))
      (message file)
      (delete-file file))))
#+end_src
** Mode line
*** Add column-number
#+begin_src emacs-lisp
(column-number-mode)
#+end_src
** Scroll behaviour
*** Smoother scrolling with keyboard
#+begin_src emacs-lisp
(setq redisplay-dont-pause t
      scroll-margin 1
      scroll-step 1
      scroll-conservatively 10000
      scroll-preserve-screen-position 1)
#+end_src
*** Smoother scrolling with mouse
#+begin_src emacs-lisp
(setq mouse-wheel-follow-mouse 't)
(setq mouse-wheel-scroll-amount '(1 ((shift) . 1)))
#+end_src
* Whitespace rules
** Use spaces, not tabs
#+begin_src emacs-lisp
(setq-default indent-tabs-mode nil)
#+end_src
** On save, add newline at end and delete trailing whitespace
#+begin_src emacs-lisp
(defun my-delete-trailing-whitespace ()
  (if (eq indent-tabs-mode nil)
      (delete-trailing-whitespace)))
(add-hook 'before-save-hook 'my-delete-trailing-whitespace)
(setq require-final-newline t)
#+end_src
** One space after period
#+begin_src emacs-lisp
(setq sentence-end-double-space nil)
#+end_src
** Two space tabs
#+begin_src emacs-lisp
(setq default-tab-width 2)
#+end_src
** Wrap at column 80, mark at 80 (soft) and 100 (hard)
#+begin_src emacs-lisp
(setq-default fill-column column-wrap-soft)
(autoload 'column-marker-2 "column-marker" "Highlight a column." t)
(add-hook 'c++-mode-hook (lambda () (interactive) (column-marker-1 column-wrap-soft)))
(add-hook 'c++-mode-hook (lambda () (interactive) (column-marker-2 column-wrap-hard)))
#+end_src
* Global key mappings
#+begin_src emacs-lisp
(global-set-key "\C-z" 'undo)
(global-set-key "\C-o" 'goto-line)
(global-set-key "\M-r" 'replace-string)
(global-set-key "\M-k" 'compile)
(global-set-key "\C-cz" 'goto-last-change)
(global-set-key [(meta return)] 'toggle-max-frame)
#+end_src
** Turn off insert
#+begin_src emacs-lisp
(global-set-key [insert] (lambda () (interactive)))
(global-set-key [insertchar] (lambda () (interactive)))
#+end_src
** Access killring menu
#+begin_src emacs-lisp
(global-set-key "\C-cy" '(lambda () (interactive) (popup-menu 'yank-menu)))
#+end_src
** Cycle buffers with F7/F8
#+begin_src emacs-lisp
(global-set-key [f7] 'previous-buffer)
(global-set-key [f8] 'next-buffer)
#+end_src
** Cycle windows with F5/F6
#+begin_src emacs-lisp
(global-set-key [f5] 'other-window)
(global-set-key [f6] (lambda () (interactive) (other-window -1)))
#+end_src
** Windmove with C-c <arrow>
#+begin_src emacs-lisp
(global-set-key (kbd "C-c <left>")  'windmove-left)
(global-set-key (kbd "C-c <right>") 'windmove-right)
(global-set-key (kbd "C-c <up>")    'windmove-up)
(global-set-key (kbd "C-c <down>")  'windmove-down)
#+end_src
** Home cycles between beginning of line and indent
#+begin_src emacs-lisp
(defun beginning-of-line-or-indentation ()
  "move to beginning of line, or indentation"
  (interactive)
  (if (bolp)
      (back-to-indentation)
    (beginning-of-line)))

(global-set-key [home] 'beginning-of-line-or-indentation)
#+end_src
** Tab behaviour
#+begin_src emacs-lisp
(global-set-key (kbd "TAB") 'smart-tab)
#+end_src
** Minimap toggle
#+begin_src emacs-lisp
(defun minimap-toggle ()
  "Toggle minimap for current buffer."
  (interactive)
  (if (not (boundp 'minimap-bufname))
      (setf minimap-bufname nil))
  (if (null minimap-bufname)
      (progn (minimap-create)
	     (set-frame-width (selected-frame) (+ (/ (* column-wrap-hard 5) 4) 4)))
    (progn (minimap-kill)
	   (set-frame-width (selected-frame) column-wrap-hard))))
(global-set-key "\M-m" 'minimap-toggle)
#+end_src
** Highlight symbol
#+begin_src emacs-lisp
(global-set-key [(control f3)] 'highlight-symbol-at-point)
(global-set-key [f3] 'highlight-symbol-next)
(global-set-key [(shift f3)] 'highlight-symbol-prev)
#+end_src
** Bookmarks
#+begin_src emacs-lisp
(global-set-key [(control f2)] 'bm-toggle)
(global-set-key [f2] 'bm-next)
(global-set-key [(shift f2)] 'bm-previous)
#+end_src
* Auto modes
#+begin_src emacs-lisp
(setq auto-mode-alist (append '(("\\.mm$" . objc-mode)
                                ("\\.h$" . c++-mode)
                                ("\\.hs$" . haskell-mode)
                                ("SConstruct" . python-mode)
                                ("SConscript" . python-mode)
                                ("\\.presql$" . sql-mode)
                                ("\\.sql$" . sql-mode)
                                ("\\.lua$" . lua-mode)
                                ("\\.ml[iyl]?$" . caml-mode)
                                ("\\.pb$" . protobuf-mode)
                                ("\\.cs$" . csharp-mode)
                                ("\\.qml$" . js2-mode)
                                ("\\.ui$" . nxml-mode)
                                ("\\.org$" . org-mode)
                                ("\\.json$" . json-mode)
                                ("\\.proto$" . protobuf-mode))
                              auto-mode-alist))
#+end_src
** Guess mode for new buffer
#+begin_src emacs-lisp
(setq default-major-mode
      (lambda () (let ((buffer-file-name (or buffer-file-name (buffer-name))))
                   (set-auto-mode))))
#+end_src
* Colors
** Syntax highlighting customizations
#+begin_src emacs-lisp
(defun color-customizations ()
  (set-face-foreground 'font-lock-comment-face "gray")
  (set-face-foreground 'font-lock-string-face "FireBrick")
  (set-face-foreground 'font-lock-warning-face "black")
  (set-face-background 'font-lock-warning-face "orange")
  (set-face-background 'region "moccasin")
  (set-face-foreground 'region "navy"))
#+end_src
** Color themes
#+begin_src emacs-lisp
(defun normal-color-theme()
  (interactive)
  (color-theme-standard)
  (color-customizations))
(defun dark-color-theme()
  (interactive)
  (color-theme-midnight))
#+end_src
* Dired customizations
#+begin_src emacs-lisp
(add-hook 'dired-mode-hook 'hl-line-mode)
#+end_src
* Utility functions
** Insert date/time
#+begin_src emacs-lisp
(defvar current-date-time-format "%a %b %d %H:%M:%S %Y"
  "Format of date to insert with `insert-current-date-time' func
See help of `format-time-string' for possible replacements")

(defvar current-time-format "%a %H:%M:%S"
  "Format of date to insert with `insert-current-time' func.
Note the weekly scope of the command's precision.")

(defun insert-current-date-time ()
  "insert the current date and time into current buffer.
Uses `current-date-time-format' for the formatting the date/time."
  (interactive)
  (insert (format-time-string current-date-time-format (current-time)))
  (insert "\n")
  )

(defun insert-current-time ()
  "insert the current time (1-week scope) into the current buffer."
  (interactive)
  (insert (format-time-string current-time-format (current-time)))
  (insert "\n")
  )

(global-set-key "\C-c\C-d" 'insert-current-date-time)
(global-set-key "\C-c\C-t" 'insert-current-time)
#+end_src
** Nuke all buffers (except scratch)
#+begin_src emacs-lisp
(defun nuke-all-buffers ()
  "kill all buffers, leaving *scratch* only"
  (interactive)
  (mapcar (lambda (x) (kill-buffer x))
    (buffer-list))
  (delete-other-windows))
#+end_src
** ANSI colors
#+begin_src emacs-lisp
(require 'ansi-color)
(require 'tty-format)
(add-hook 'find-file-hooks 'tty-format-guess)
(defun colorize-buffer ()
  "apply ansi color codes to current buffer"
  (interactive)
  (ansi-color-apply-on-region (point-min) (point-max)))
#+end_src
** Eval last sexp and replace with result
#+begin_src emacs-lisp
(defun eval-and-replace ()
  "Replace the preceding sexp with its value."
  (interactive)
  (backward-kill-sexp)
  (condition-case nil
      (prin1 (eval (read (current-kill 0)))
             (current-buffer))
    (error (message "Invalid expression")
           (insert (current-kill 0)))))
(global-set-key (kbd "C-x C-S-e") 'eval-and-replace)
;(global-set-key (kbd "C-x C-e") 'eval-last-sexp)
#+end_src
* Project-specific indent rules
#+begin_src emacs-lisp
(defun normal-indent-rules ()
  (interactive)
  (setq indent-tabs-mode nil)
  (setq default-tab-width 2)
  (setq c-basic-offset 2)
  (setq c-basic-indent 2))
(defun tabbed-indent-rules ()
  (interactive)
  (setq indent-tabs-mode t)
  (setq default-tab-width 4)
  (setq c-basic-offset 4)
  (setq c-basic-indent 4))
#+end_src
* Language modes
** C/C++
*** 2-space basic offset
#+begin_src emacs-lisp
(setq-default c-basic-offset 2)
#+end_src
*** FIXME or TODO get highlighted
#+begin_src emacs-lisp
(font-lock-add-keywords 'c++-mode
  '(("\\<\\(FIXME\\|TODO\\).*?:" 0 font-lock-warning-face prepend)))
#+end_src
*** Common indentation rules
#+begin_src emacs-lisp
(defun indentation-c++-mode-hook ()
  (c-set-offset 'substatement-open 0)
  (c-set-offset 'brace-list-open 0)
  (c-set-offset 'member-init-cont '-)
  (c-set-offset 'arglist-intro '++)
  (c-set-offset 'case-label '+)
  (c-set-offset 'statement-case-open 0))
(add-hook 'c++-mode-hook 'indentation-c++-mode-hook)
#+end_src
*** Infer tabs or spaces
#+begin_src emacs-lisp
(defun how-many-region (begin end regexp &optional interactive)
  "Print number of non-trivial matches for REGEXP in region.
  Non-interactive arguments are Begin End Regexp"
  (interactive "r\nsHow many matches for (regexp): \np")
  (let ((count 0) opoint)
    (save-excursion
      (setq end (or end (point-max)))
      (goto-char (or begin (point)))
      (while (and (< (setq opoint (point)) end)
                  (re-search-forward regexp end t))
        (if (= opoint (point))
            (forward-char 1)
          (setq count (1+ count))))
      (if interactive (message "%d occurrences" count))
      count)))
(defun infer-indentation-style ()
  ;; if our source file uses tabs, we use tabs, if spaces spaces, and if
  ;; neither, we use the current indent-tabs-mode
  (let ((space-count (how-many-region (point-min) (point-max) "^  "))
        (tab-count (how-many-region (point-min) (point-max) "^\t")))
    (if (> space-count tab-count) (normal-indent-rules))
    (if (> tab-count space-count) (tabbed-indent-rules))))
(add-hook 'c++-mode-hook 'infer-indentation-style)
#+end_src
*** Fill paras properly with doxygen comments
#+begin_src emacs-lisp
(defun setup-doxygen-fill ()
  (setq paragraph-separate "^\\s-*//!?\\s-*$"))
(add-hook 'c++-mode-hook 'setup-doxygen-fill)
#+end_src
** Python
*** 2-space basic offset
#+begin_src emacs-lisp
(setq python-indent 2)
#+end_src
*** FIXME or TODO get highlighted
#+begin_src emacs-lisp
(font-lock-add-keywords 'python-mode
  '(("\\<\\(FIXME\\|TODO\\).*?:" 0 font-lock-warning-face prepend)))
#+end_src
** Ocaml
#+begin_src emacs-lisp
(add-hook 'caml-mode-hook (lambda() (require 'caml-font)))
#+end_src
** Lua
*** 2-space basic offset
#+begin_src emacs-lisp
(setq lua-indent-level 2)
#+end_src
** Javascript
*** 2-space basic offset
#+begin_src emacs-lisp
(setq js-indent-level 2)
#+end_src
** SQL
#+begin_src emacs-lisp
(font-lock-add-keywords 'sql-mode
  '(("\\s-*//.*$" 0 font-lock-comment-face prepend)))
#+end_src
** Protobufs
#+begin_src emacs-lisp
(autoload 'protobuf-mode "protobuf-mode" "Protocol buffer editing mode" t)
#+end_src
** Common Lisp
*** Use SLIME with lisp files
#+begin_src emacs-lisp
(add-hook 'lisp-mode-hook
          (lambda ()
            (slime-mode t)
            (local-set-key "\r" 'newline-and-indent)
            (setq lisp-indent-function 'common-lisp-indent-function)))
#+end_src
** Emacs Lisp
*** Byte-compile elisp files on save
#+begin_src emacs-lisp
(defun byte-compile-current-buffer ()
  "`byte-compile' current buffer if it's emacs-lisp-mode and compiled file exists."
  (interactive)
  (when (and (eq major-mode 'emacs-lisp-mode)
             (file-exists-p (byte-compile-dest-file buffer-file-name)))
    (byte-compile-file buffer-file-name)))

(add-hook 'after-save-hook 'byte-compile-current-buffer)
#+end_src
** Haskell
#+begin_src emacs-lisp
(eval-after-load "haskell-mode"
  '(define-key haskell-mode-map (kbd "M-k") 'haskell-compile))
(eval-after-load "haskell-cabal"
  '(define-key haskell-cabal-mode-map (kbd "M-k") 'haskell-compile))
(eval-after-load "haskell-mode"
  '(define-key haskell-mode-map (kbd "C-c v c") 'haskell-cabal-visit-file))
(eval-after-load "haskell-mode"
  '(progn
    (define-key haskell-mode-map (kbd "C-c C-z") 'haskell-interactive-switch)
    (define-key haskell-mode-map (kbd "C-c C-l") 'haskell-process-load-file)
    (define-key haskell-mode-map (kbd "C-c C-b") 'haskell-interactive-switch)
    (define-key haskell-mode-map (kbd "C-c C-t") 'haskell-process-do-type)
    (define-key haskell-mode-map (kbd "C-c C-i") 'haskell-process-do-info)))
(add-hook 'haskell-mode-hook 'turn-on-haskell-indentation)
(add-hook 'haskell-mode-hook 'flycheck-mode)
(add-hook 'flycheck-mode-hook #'flycheck-haskell-setup)
#+end_src
* Compiling
** Scroll compilation window to end
#+begin_src emacs-lisp
(setq compilation-scroll-output t)
#+end_src
** M-up and M-down to go between errors
#+begin_src emacs-lisp
(global-set-key [\M-up] 'previous-error)
(global-set-key [\M-down] 'next-error)
#+end_src
** Scons
*** Map compile errors from build dir to src dir
#+begin_src emacs-lisp
;; SCons builds into a 'build' subdir, but we want to find the errors
;; in the regular source dir.  So we remove build/XXX/YYY/{dbg,final}/ from the
;; filenames.
(defun process-error-filename (filename)
  (let ((case-fold-search t))
    (setq f (replace-regexp-in-string
             "[Ss]?[Bb]uild[\\/].*\\(final\\|dbg\\)[^\\/]*[\\/]" "" filename))
    (cond ((file-exists-p f)
           f)
          (t filename))))
(setq compilation-parse-errors-filename-function 'process-error-filename)
#+end_src
*** On compile, search for SConstruct in parent directories recursively
#+begin_src emacs-lisp
;; inspired by jds-find-tags-file in http://www.emacswiki.org/emacs/EmacsTags
(defun find-sconstruct ()
  "recursively searches upwards from buffer's current dir for file named SConstruct and returns that dir. Or nil if not found or if buffer is not visiting a file"
  (labels
      ((find-sconstruct-r (path)
                          (let* ((parent (file-name-directory path))
                                 (possible-file (concat parent "SConstruct")))
                            (cond
                             ((file-exists-p possible-file)
                              (throw 'found-it possible-file))
                             ((string= "/SConstruct" possible-file)
                              (error "No SConstruct found"))
                             (t (find-sconstruct-r (directory-file-name parent)))))))
    (if (buffer-file-name)
        (catch 'found-it
          (find-sconstruct-r (buffer-file-name)))
      (error "Buffer is not visiting a file"))))

(defun project-root ()
  (file-name-directory (find-sconstruct)))

(if (eq system-type 'cygwin)
    (setq compile-command '(concat "cd " (project-root) " && /usr/local/bin/scons"))
    (setq compile-command '(concat "cd " (project-root) " && scons")))
(setq compilation-read-command nil)
#+end_src

* Tools
** Git
*** Use magit
#+begin_src emacs-lisp
(global-set-key
 "\C-cg"
 (lambda ()
   (interactive)
   (call-interactively 'magit-status)))
#+end_src
*** Use mo-git-blame
#+begin_src emacs-lisp
(defun my-mo-git-blame-quit ()
  (interactive)
  (mo-git-blame-quit)
  (setup-normal-layout))

(global-set-key
  "\C-cb"
  (lambda ()
    (interactive)
    (setup-doublewide-layout)
    (call-interactively 'mo-git-blame-current)
    (define-key mo-git-blame-mode-map (kbd "q") 'my-mo-git-blame-quit)
    (define-key mo-git-blame-content-mode-map (kbd "q") 'my-mo-git-blame-quit)))
#+end_src
** Doxygen
#+begin_src emacs-lisp
(require 'doxygen)
(global-set-key "\C-cd" 'doxygen-insert-function-comment)
#+end_src
** Uniquify filenames with <>
#+begin_src emacs-lisp
(require 'uniquify)
(setq uniquify-buffer-name-style 'post-forward-angle-brackets)
#+end_src
** Dabbrev customization
#+begin_src emacs-lisp
(setq save-abbrevs nil)
(setq case-replace nil)
#+end_src
** Recent files
*** Enable recentf mode
Also limit to 25 files
#+begin_src emacs-lisp
(require 'recentf)
(setq recentf-max-menu-items 25)
#+end_src

*** Don't clean up recent files
It potentially access network drives, which could trigger tramp,
http://stackoverflow.com/questions/880625

#+begin_src emacs-lisp
(setq recentf-auto-cleanup 'never)
#+end_src

*** Bind to C-x C-r
"C-x r" would be better, but it interfers with "C-x r k" for kill-rectangle
#+begin_src emacs-lisp
(global-set-key "\C-x\ \C-r" 'recentf-open-files)
#+end_src
* Shell things
** Make C-c s a prefix for shells
#+begin_src emacs-lisp
(global-set-key (kbd "C-c s") (make-sparse-keymap))
#+end_src
** Names of shell buffers
#+begin_src emacs-lisp
(defvar my-local-shells
  '("*eshell*" "*shell*" "*shell0*" "*shell1*" "*shell2*" "*shell3*"))
#+end_src
** Truncate buffers continuously
#+begin_src emacs-lisp
(add-hook 'comint-output-filter-functions 'comint-truncate-buffer)
#+end_src
** Make the output read only
#+begin_src emacs-lisp
(defun make-my-shell-output-read-only (text)
  "Add to comint-output-filter-functions to make stdout read only in my shells."
  (if (member (buffer-name) my-local-shells)
      (let ((inhibit-read-only t)
            (output-end (process-mark (get-buffer-process (current-buffer)))))
        (put-text-property comint-last-output-start output-end 'read-only t))))
(add-hook 'comint-output-filter-functions 'make-my-shell-output-read-only)
#+end_src
** Colour in shells
#+begin_src emacs-lisp
(add-hook 'shell-mode-hook 'ansi-color-for-comint-mode-on)
#+end_src
** Open a bash shell
#+begin_src emacs-lisp
(defun bash-shell ()
  "Opens a bash shell."
  (interactive)
  (let ((explicit-shell-file-name "bash")
        (explicit-bash-args '("--login" "-i")))
    (call-interactively 'shell)))
(global-set-key (kbd "C-c s b") 'bash-shell)
#+end_src
** Open an MSVC 2010 shell
#+begin_src emacs-lisp
(defun msvc-2010-shell ()
  "Opens a command shell with MSVC 2010 environment."
  (interactive)
  (let ((explicit-shell-file-name "cmd")
        (explicit-cmd-args '("/k" "C:\\Program Files (x86)\\Microsoft Visual Studio 10.0\\VC\\vcvarsall.bat" "x86")))
    (call-interactively 'shell)))
(global-set-key (kbd "C-c s m") 'msvc-2010-shell)
#+end_src
** Open an ansi-term shell
#+begin_src emacs-lisp
(global-set-key (kbd "C-c s a") (lambda () (interactive) (ansi-term "/bin/bash")))
#+end_src
** Open a term shell
#+begin_src emacs-lisp
(global-set-key (kbd "C-c s t") (lambda () (interactive) (term "/bin/bash")))
#+end_src
** Open an eshell
#+begin_src emacs-lisp
(global-set-key (kbd "C-c s e") 'eshell)
#+end_src
** Repeat last shell buffer input
#+begin_src emacs-lisp
(defun repeat-last-command-in-eshell ()
  "Repeat the last command that the *eshell* buffer did."
  (interactive)
  (let ((buf (current-buffer)))
    (if (get-buffer "*eshell*")
        (progn
          (display-buffer "*eshell*" t)
          (switch-to-buffer-other-window "*eshell*")
          (end-of-buffer)
          (eshell-kill-input)
          (insert (eshell-get-history 0))
          (eshell-send-input)
          (end-of-buffer)
          (switch-to-buffer-other-window buf)))))

(defun repeat-last-command-in-comint-shell ()
  "Repeat the last command that the *shell* buffer did."
  (interactive)
  (with-current-buffer (get-buffer "*shell*")
    (comint-previous-input 0)
    (comint-send-input nil t)))

(defun repeat-last-command-in-shell ()
  "Repeat the last command that the (e)shell buffer did."
  (interactive)
  (cond ((get-buffer "*eshell*")
         (repeat-last-command-in-eshell))
        ((get-buffer "*shell*")
         (repeat-last-command-in-comint-shell))))

(global-set-key (kbd "C-c r") 'repeat-last-command-in-shell)
#+end_src
** Use external ls
#+begin_src emacs-lisp
(setq ls-lisp-use-insert-directory-program t)
(setq insert-directory-program "/bin/ls")
#+end_src
** Run test on successful compilation
#+begin_src emacs-lisp
;(setq compilation-finish-functions 'compile-autoclose)
(defun compile-autoclose (buffer string)
  (cond ((string-match "finished" string)
         (message "Build maybe successful: running tests.")
         (run-with-timer 1 nil 'repeat-last-command-in-shell))))
#+end_src
** Open shell at current buffer's directory
#+begin_src emacs-lisp
(defun eshell-here ()
  "Opens up a new shell in the directory associated with the current buffer's file."
  (interactive)
  (let* ((parent (file-name-directory (buffer-file-name)))
         (name   (car
                  (last
                   (split-string parent "/" t)))))
    (split-window-vertically)
    (other-window 1)
    (eshell "new")
    (rename-buffer (concat "*eshell: " name "*"))
    (insert (concat "ls"))
    (eshell-send-input)))
(global-set-key (kbd "C-c s E") 'eshell-here)

(defun delete-single-window (&optional window)
  "Remove WINDOW from the display.  Default is `selected-window'."
  (interactive)
  (save-current-buffer
    (setq window (or window (selected-window)))
    (select-window window)
    (kill-buffer)
    (if (not (one-window-p t))
        (delete-window (selected-window)))))

(defun eshell/x (&rest args)
  (delete-single-window))
#+end_src
* Org mode
#+begin_src emacs-lisp
(require 'org-install)
(define-key global-map "\C-cl" 'org-store-link)
(define-key global-map "\C-cc" 'org-capture)
(define-key global-map "\C-ca" 'org-agenda)
(define-key global-map "\C-ci" 'org-iswitchb)
(setq org-log-done t)
(setq org-support-shift-select t)
(add-hook 'org-mode-hook
  (lambda ()
    (define-key org-mode-map "\M-Q" 'toggle-truncate-lines)))
(setq org-startup-indented t)
(setq org-src-fontify-natively t)
(setq org-completion-use-ido t)

(require 'ox-latex)
(require 'ox-beamer)
(add-to-list 'org-latex-packages-alist '("" "minted" nil))
(setq org-latex-listings 'minted)
(setq org-latex-pdf-process
      '("pdflatex -shell-escape -interaction nonstopmode -output-directory %o %f"
        "pdflatex -shell-escape -interaction nonstopmode -output-directory %o %f"
        "pdflatex -shell-escape -interaction nonstopmode -output-directory %o %f"))

(setq org-latex-minted-options
           '(("frame" "lines")
             ("fontsize" "\\scriptsize")
             ("linenos" "")))
#+end_src
* CEDET
see https://github.com/alexott/emacs-configs/blob/master/rc/emacs-rc-cedet.el
#+begin_src emacs-lisp
;; if using a local cedet version, try that, otherwise try a default
(if (file-accessible-directory-p cedet-dir)
    (progn
      (load-file (concat cedet-dir "cedet-devel-load.el"))
      (add-to-list 'load-path (concat cedet-dir "contrib/"))
      (add-to-list 'Info-directory-list (concat cedet-dir "doc/info/")))
    (require 'cedet))

(add-to-list 'semantic-default-submodes 'global-semantic-mru-bookmark-mode)
(add-to-list 'semantic-default-submodes 'global-semanticdb-minor-mode)
(add-to-list 'semantic-default-submodes 'global-semantic-idle-scheduler-mode)
(add-to-list 'semantic-default-submodes 'global-cedet-m3-minor-mode)
(add-to-list 'semantic-default-submodes 'global-semantic-highlight-func-mode)
;;(add-to-list 'semantic-default-submodes 'global-semantic-decoration-mode)
;;(add-to-list 'semantic-default-submodes 'global-semantic-stickyfunc-mode)
;;(add-to-list 'semantic-default-submodes 'global-semantic-idle-summary-mode)
;;(add-to-list 'semantic-default-submodes 'global-semantic-show-unmatched-syntax-mode)
;;(add-to-list 'semantic-default-submodes 'global-semantic-highlight-edits-mode)
;;(add-to-list 'semantic-default-submodes 'global-semantic-show-parser-state-mode)

;; Activate semantic
(semantic-mode 1)

(require 'semantic/bovine/c)
(require 'semantic/bovine/el)
(require 'semantic/bovine/gcc)
(require 'semantic/bovine/clang)
(require 'semantic/ia)
(require 'semantic/decorate)
(require 'semantic/decorate/include)
(require 'semantic/lex-spp)
(require 'eassist)

;; customisation of modes
(defun my-c-mode-cedet-hook ()
  (local-set-key [(control return)] 'semantic-ia-complete-symbol-menu)
  ;;(local-set-key "\C-c?" 'semantic-ia-complete-symbol)
  ;;(local-set-key "\C-c>" 'semantic-complete-analyze-inline)

  (local-set-key "\C-c#" 'semantic-decoration-include-visit)

  (local-set-key "\C-cj" 'semantic-ia-fast-jump)
  (local-set-key "\C-co" 'semantic-analyze-proto-impl-toggle)
  (local-set-key (kbd "C-c <left>") 'senator-fold-tag)
  (local-set-key (kbd "C-c <right>") 'senator-unfold-tag)

  (local-set-key [f12] 'semantic-ia-fast-jump)
  (local-set-key [(shift f12)] 'semantic-mrub-switch-tags)

  ;;(local-set-key "." 'semantic-complete-self-insert)
  ;;(local-set-key ">" 'semantic-complete-self-insert)
  (local-set-key "\C-x\C-h" 'eassist-switch-h-cpp)
  (local-set-key "\C-cm" 'eassist-list-methods)
  (local-set-key "\C-c\C-r" 'semantic-symref))

(add-hook 'c-mode-common-hook 'my-c-mode-cedet-hook)

(semanticdb-enable-gnu-global-databases 'c-mode t)
(semanticdb-enable-gnu-global-databases 'c++-mode t)

(when (cedet-ectag-version-check t)
  (semantic-load-enable-primary-ectags-support))

;; EDE
(global-ede-mode 1)
(ede-enable-generic-projects)
#+end_src
* Multiple cursors
#+begin_src emacs-lisp
(global-set-key (kbd "C->") 'mc/mark-next-like-this)
(global-set-key (kbd "C-<") 'mc/mark-previous-like-this)
(global-set-key (kbd "C-c C-<") 'mc/mark-all-like-this)
#+end_src
* Window layouts
** For screencasting
#+begin_src emacs-lisp
(setq fullscreen-screencast nil)
(defun setup-screencasting-layout ()
  (interactive)
  (require 'frame-fns)
  (delete-other-windows)
  (if fullscreen-screencast
    (progn
      (maximize-frame)
      (split-window-horizontally -80))
    (progn
      (shell-command (concat "wmctrl -r "
                       (car (split-string (get-frame-name)))
                       " -b remove,maximized_vert,maximized_horz"))
      (shell-command (concat "wmctrl -r "
                       (car (split-string (get-frame-name)))
                       " -e 0,100,100,1280,720"))
      (split-window-horizontally -50)))
  (other-window 1)
  (switch-to-buffer "*compilation*")
  (split-window-vertically)
  (other-window 1)
  (eshell)
  (other-window 1)
  (if fullscreen-screencast
    (text-scale-set 2)
    (text-scale-set 1)))
#+end_src
** Normal mode
#+begin_src emacs-lisp
(defun setup-normal-layout ()
  (interactive)
  (delete-other-windows)
  (text-scale-set 0)
  (set-frame-size (selected-frame) column-wrap-hard default-frame-height))
#+end_src
** Double-wide mode, split
#+begin_src emacs-lisp
(defun setup-doublewide-layout ()
  (interactive)
  (delete-other-windows)
  (text-scale-set 0)
  (set-frame-size (selected-frame) (+ 3 (* 2 column-wrap-hard)) default-frame-height)
  (split-window-horizontally))
#+end_src
** With compilation/shell buffers
#+begin_src emacs-lisp
(defun setup-dev-layout ()
  (interactive)
  (delete-other-windows)
  (text-scale-set 0)
  (set-frame-size (selected-frame) (+ 3 column-wrap-hard column-wrap-soft) default-frame-height)
  (split-window-horizontally (+ 3 column-wrap-hard))
  (other-window 1)
  (switch-to-buffer "*compilation*")
  (split-window-vertically 40)
  (other-window 1)
  (eshell)
  (other-window 1))
#+end_src
** Expand to double wide, keep split if already
#+begin_src emacs-lisp
(defun setup-doublewide-layout-keep-split ()
  (interactive)
  (set-frame-size (selected-frame) (+ 3 (* 2 column-wrap-hard)) default-frame-height)
  (when (= (length (window-list)) 1)
    (split-window-horizontally)))
#+end_src
** Dock frame to screen edge
#+begin_src emacs-lisp
(defun dock-frame-right ()
  (interactive)
  (if (eq system-type 'gnu/linux)
    (progn (require 'frame-fns)
      (shell-command
        (concat dotfile-dir ".emacs.d/site-bin/dockwindow.sh "
               (car (split-string (get-frame-name))) " right")))
    (set-frame-position (selected-frame) -1 0)))
(defun dock-frame-left ()
  (interactive)
  (if (eq system-type 'gnu/linux)
    (progn (require 'frame-fns)
      (shell-command
        (concat dotfile-dir ".emacs.d/site-bin/dockwindow.sh "
               (car (split-string (get-frame-name))) " left")))
    (set-frame-position (selected-frame) 0 0)))
#+end_src
** Key bindings
#+begin_src emacs-lisp
(global-set-key "\C-cw" 'setup-screencasting-layout)
(global-set-key "\C-cm" 'setup-dev-layout)
(global-set-key "\C-cn" 'setup-normal-layout)
(global-set-key "\C-c1"
  (lambda () (interactive) (setup-normal-layout) (dock-frame-right)))
(global-set-key "\C-c2"
  (lambda () (interactive) (setup-doublewide-layout-keep-split) (dock-frame-right)))
(global-set-key "\C-c9" 'dock-frame-left)
(global-set-key "\C-c0" 'dock-frame-right)
#+end_src
* Lilypond
#+begin_src emacs-lisp
(if (eq system-type 'gnu/linux)
  (progn
    (autoload 'LilyPond-mode "lilypond-mode")
    (setq auto-mode-alist (cons '("\\.ly$" . LilyPond-mode) auto-mode-alist))
    (add-hook 'LilyPond-mode-hook (lambda () (turn-on-font-lock)))))
#+end_src
* ace jump
#+begin_src emacs-lisp
(autoload
  'ace-jump-mode
  "ace-jump-mode"
  "Emacs quick move minor mode"
  t)
(define-key global-map (kbd "C-c SPC") 'ace-jump-mode)

(autoload
  'ace-jump-mode-pop-mark
  "ace-jump-mode"
  "Ace jump back:-)"
  t)
(eval-after-load "ace-jump-mode"
  '(ace-jump-mode-enable-mark-sync))
(define-key global-map (kbd "C-x SPC") 'ace-jump-mode-pop-mark)
#+end_src
* show parens
#+begin_src emacs-lisp
(show-paren-mode 1)
(setq show-paren-delay 0)
#+end_src
* Start server
#+begin_src emacs-lisp
(require 'server)
(when (and (= emacs-major-version 24)
           (= emacs-minor-version 1)
           (eq system-type 'windows-nt))
  (defun server-ensure-safe-dir (dir) "Noop" t)) ; Suppress error "directory
                                        ; ~/.emacs.d/server is unsafe"
                                        ; on windows.
(server-start)
#+end_src
* Colors
** Color themes
#+begin_src emacs-lisp
(require 'color-theme)
(color-theme-initialize)
(normal-color-theme)
#+end_src
* SLIME
#+begin_src emacs-lisp
(require 'slime)
#+end_src
** Ignore protocol mismatches
#+begin_src emacs-lisp
(setq slime-protocol-version 'ignore)
#+end_src
** Fancy completion
#+begin_src emacs-lisp
(setq slime-complete-symbol*-fancy t)
(setq slime-complete-symbol-function 'slime-fuzzy-complete-symbol)
#+end_src
** Load SLIME
#+begin_src emacs-lisp
(slime-setup '(slime-fancy slime-asdf slime-banner))
#+end_src
* Cygwin mount points
#+begin_src emacs-lisp
(if (eq system-type 'windows-nt)
    (progn
      (require 'cygwin-mount)
      (cygwin-mount-activate)))
(if (eq system-type 'cygwin)
    (progn
      (require 'windows-path)
      (windows-path-activate)))
#+end_src
* Undo-tree
#+begin_src emacs-lisp
(require 'undo-tree)
(global-undo-tree-mode 1)
#+end_src
* IDO
** Use flx-ido
#+begin_src emacs-lisp
(require 'flx-ido)
(ido-mode 1)
(setq ido-everywhere t)
(setq ido-use-filename-at-point nil) ;'guess)
(setq ido-create-new-buffer 'always)
(flx-ido-mode 1)
(setq ido-use-faces nil)
;(setq flx-ido-use-faces nil)
#+end_src
** Ubiqitous
#+begin_src emacs-lisp
(require 'ido-ubiquitous)
(ido-ubiquitous-mode)
#+end_src
** IDO support for M-x
#+begin_src emacs-lisp
(require 'smex)
(smex-initialize)
(global-set-key (kbd "M-x") 'smex)
#+end_src
* Nyancat mode-line!
#+begin_src emacs-lisp
;(nyan-mode)
;(nyan-start-animation)
#+end_src
* Yasnippet config
#+begin_src emacs-lisp
(require 'yasnippet)
(yas-global-mode 1)

(require 'dropdown-list)
(setq yas-prompt-functions '(yas-dropdown-prompt
                             yas-ido-prompt
                             yas-completing-prompt))
#+end_src
* Autocomplete
#+begin_src emacs-lisp
(require 'auto-complete-config)
(add-to-list 'ac-dictionary-directories "~/.emacs.d/ac-dict")
(ac-config-default)
(ac-set-trigger-key "TAB")
(ac-set-trigger-key "<tab>")
#+end_src
* Projectile
#+begin_src emacs-lisp
(require 'projectile)
(projectile-global-mode)
(setq projectile-enable-caching t)
(if (eq system-type 'gnu/linux)
    (setq projectile-tags-command "ctags-exuberant -Re %s %s"))
#+end_src
* Tags
** Use etags stuff
#+begin_src emacs-lisp
(require 'etags-select)
(require 'etags-table)
(setq tags-revert-without-query 1)
(setq etags-table-search-up-depth 10)
#+end_src
** Find tag or include
#+begin_src emacs-lisp
(defun find-tag-or-include ()
  "If the point is on an include line, visit that file. Else do a
normal find-tag."
  (interactive)
  (condition-case nil
      (semantic-decoration-include-visit)
    (error (etags-select-find-tag-at-point))))
(global-set-key "\M-." 'find-tag-or-include)
#+end_src
* git-gutter-fringe
#+begin_src emacs-lisp
(require 'git-gutter-fringe+)
(global-git-gutter+-mode 1)
(setq git-gutter-fr+-side 'right-fringe)
#+end_src
* ecb
#+begin_src emacs-lisp
(require 'ecb)
(require 'ecb-autoloads)
(setq ecb-show-sources-in-directories-buffer 'always)

;;; activate and deactivate ecb
(defun my/ecb-activate ()
  (interactive)
  (ecb-activate)
  (set-frame-size (selected-frame) (+ column-wrap-hard 48) default-frame-height))
(defun my/ecb-deactivate ()
  (interactive)
  (ecb-deactivate)
  (set-frame-size (selected-frame) column-wrap-hard default-frame-height))
(global-set-key (kbd "C-c C-;") 'my/ecb-activate)
(global-set-key (kbd "C-c C-'") 'my/ecb-deactivate)

;;; quick navigation between ecb windows
(global-set-key (kbd "C-)") 'ecb-goto-window-edit1)
(global-set-key (kbd "C-!") 'ecb-goto-window-directories)
(global-set-key (kbd "C-\"") 'ecb-goto-window-sources)
(global-set-key (kbd "C-Â£") 'ecb-goto-window-methods)
(global-set-key (kbd "C-$") 'ecb-goto-window-compilation)
#+end_src
* smart mode line
#+begin_src emacs-lisp
(require 'smart-mode-line)
(sml/setup)
(sml/apply-theme 'light)
#+end_src
* ediff
#+begin_src emacs-lisp
(require 'ediff)
(add-hook 'ediff-before-setup-hook 'new-frame)
(add-hook 'ediff-quit-hook 'delete-frame)
(setq ediff-window-setup-function 'ediff-setup-windows-plain)
(setq ediff-split-window-function 'split-window-vertically)
#+end_src
